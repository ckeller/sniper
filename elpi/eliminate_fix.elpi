% returns the recursive argument of a fixpoint
pred index_struct_argument i:term, o:int.
    index_struct_argument (fix _ N _ _) N.
    index_struct_argument (fun _ _ F) N1 :-
        pi x\ index_struct_argument (F x) N, % get the body and recurse
        N1 is N + 1.

% builds the equality between two terms 
% (applied to the same list of terms)
pred mkEq_aux i: term, i: term, i: term, i: list term, o: term.
    mkEq_aux T1 T2 (prod Na Ty F) L (prod Na Ty R) :- pi x\ decl x Na Ty =>
        mkEq_aux T1 T2 (F x) [x | L]  (R x).
    mkEq_aux T1 T2 T3 L (app [ {{ @eq }}, T3, app [T1|L'], app [T2| L'] ]) :- std.rev L L'.

pred mkEq i: term, i: term, o: term.
    mkEq T1 T2 R :- coq.typecheck T2 T3 ok, mkEq_aux T1 T2 T3 [] R.

pred subst_anon_fix i: term, i: term, o: term.
    subst_anon_fix (fun Na Ty F) T2 (fun Na Ty R) :- pi x\ decl x Na Ty =>
        subst_anon_fix (F x) (app [T2, x]) (R x).
    subst_anon_fix (fix Na I Ty F) T2 T3 :- coq.typecheck (fix Na I Ty F) TyF ok,
        @holes! => 
        coq.elaborate-skeleton (F T2) TyF T3 ok.

pred is_fix i: term.
    is_fix (fix _ _ _ _).
    is_fix (fun N Ty F) :- pi x\ decl x N Ty => is_fix (F x).

pred recover_types i: (list term), o: (list (pair term term)).
    recover_types [X | XS] [pr X Ty | R] :- coq.typecheck X Ty ok, 
        recover_types XS R.
    recover_types [] [].

type abs term.

pred bind i: bool, i: term, i: term, o: term.
    bind tt T Ty T1 :- T1 = {{ fun (x : lp:Ty) => lp:(B x) }},
        pi x\ (copy (abs) x :- !) => bind ff T Ty (B x).
    bind ff T _ T1 :- copy T T1.

pred abstract i: term, i: list (pair term term), o: term.
    abstract T [pr X Ty | XS] R :- (copy X abs :- !) => copy T T1, 
        bind tt T1 Ty T2,
        abstract T2 XS R.
    abstract T [] T.

% all the subterms which are fixpoints of a function except its toplevel fix
pred subterms_fix_fun i: term, o: (list term).
    subterms_fix_fun (fun N Ty F) R :- pi x\ decl x N Ty => subterms_fix_fun (F x) R.
    subterms_fix_fun (fix N _ Ty F) R :- pi x\ decl x N Ty => subterms_fix (F x) R.

%warning: does not work with not real fixpoints (there must be a recursive call)
pred subterms_fix i: term, o: (list term). 
    subterms_fix (sort _U) [] :- !.
    subterms_fix (fun N Ty F) [fun N Ty F | R] :- is_fix (fun N Ty F),
        subterms_fix Ty R1, pi x\ decl x N Ty => subterms_fix_fun (F x) R2, % ignore the toplevel fix
        std.append R1 R2 R.  
    subterms_fix (fun N Ty F) R :- 
        subterms_fix Ty R1, pi x\ decl x N Ty => subterms_fix (F x) R2, 
        std.append R1 R2 R. 
    subterms_fix (prod N Ty F) R :- 
        subterms_fix Ty R1, pi x\ decl x N Ty =>
        subterms_fix (F x) R2,
        std.append R1 R2 R.
    subterms_fix (app L) R :- !,
        std.map L subterms_fix R',
        std.flatten R' R.
    subterms_fix (global _G) [] :- !.
    subterms_fix (let N Ty V F) R :- !,
        subterms_fix Ty R1, subterms_fix V R2, pi x\ def x N Ty V => subterms_fix (F x) R3,
        std.append R1 R2 R12,
        std.append R12 R3 R.
    subterms_fix (match T U L) R :- !, 
        subterms_fix T R1, subterms_fix U R2, std.append R1 R2 R12,
        std.map L subterms_fix R3,
        std.flatten R3 R',
        std.append R12 R' R.
    subterms_fix ((fix Na _I Ty F) as Fix) [Res | R] :- !,
        names Nms, std.filter Nms (x\ occurs x Fix) Nms',
        std.rev Nms' Nmsrev, recover_types Nmsrev NTy,
        abstract Fix NTy Res,
        subterms_fix Ty R1, pi x\ decl x Na Ty => subterms_fix (F x) R2,
        std.append R1 R2 R. % todo : abstract free variables if necessary
    subterms_fix _T [].

pred subterms_glob_const i: term, o: (list term). 
subterms_glob_const (sort U) [] :- !.
subterms_glob_const (fun N Ty F) R :- !,
    subterms_glob_const Ty R1, pi x\ decl x N Ty => subterms_glob_const (F x) R2,
    std.append R1 R2 R. 
subterms_glob_const (prod N Ty F) R :- !, 
    subterms_glob_const Ty R1, pi x\ decl x N Ty => subterms_glob_const (F x) R2,
    std.append R1 R2 R. 
subterms_glob_const (app L) R :- !,
    std.map L subterms_glob_const R',
    std.flatten R' R.
subterms_glob_const (global (const G)) [global (const G)].
subterms_glob_const (let N Ty V F) R :- !,
    subterms Ty R1, subterms V R2, pi x\ def x N Ty V => subterms_glob_const (F x) R3,
    std.append R1 R2 R12,
    std.append R12 R3 R.
subterms_glob_const (match T U L) R :- !,
    subterms_glob_const T R1, subterms_glob_const U R2, std.append R1 R2 R12,
    std.map L subterms_glob_const R3, 
    std.flatten R3 R',
    std.append R12 R' R.
subterms_glob_const (fix Na _ Ty F) R :- !,
    subterms_glob_const Ty R1, pi x\ decl x Na Ty => subterms_glob_const (F x) R2,
    std.append R1 R2 R.
subterms_glob_const _ [].

pred const_reduces_to i: term, i: term. 
    const_reduces_to (global G) T :- coq.unify-leq (global G) T ok.

pred globals_const_in_goal i: goal-ctx, o: (list term).
    globals_const_in_goal [(decl _ _ X)| L] L1 :- subterms_glob_const X L',
        globals_const_in_goal L L'', std.append L' L'' L1.
    globals_const_in_goal [(def _ _ _ X)| L] L1 :- subterms_glob_const X L',
        globals_const_in_goal L L'', std.append L' L'' L1.
    globals_const_in_goal [] [].